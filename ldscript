TARGET("elf32-tradlittlemips")
ENTRY(_start)

MEMORY
{
	flash   (rx)   : ORIGIN = 0x9fc00000,  LENGTH = 64K-64
	devcfg  (r)    : ORIGIN = 0x9fc0ffc0,  LENGTH = 16
	devsign (r)    : ORIGIN = 0x9fc0ffe0,  LENGTH = 16
	prog    (rx)   : ORIGIN = 0x9d000000,  LENGTH = 2048K
	sram    (rw!x) : ORIGIN = 0xa0000000,  LENGTH = 512K
}

SECTIONS
{
	. = 0x9fc00000;
	.start . : {
		*start.o(.text)
	} > flash

	.text : {
		*(.exception)
		*(.text)
	} > flash

	.rodata : {
		*(.rodata)
	} > flash

	.devcfg : {
		*(.config3)
		*(.config2)
		*(.config1)
		*(.config0)
	} > devcfg

	.devsign : {
		*(.devsign)
	} > devsign

	.data : {
		*(.data)
	} > sram AT > flash

	.sdata : {
		_gp = .;
		*(.sdata)
	} > sram AT > flash

	.bss : {
		*(.bss COMMON)
	} > sram

	.real_kernel : {
	} > prog

	. = ALIGN(4);
	stack_top = . + 0x1000; /* 4kB of stack memory */
}
